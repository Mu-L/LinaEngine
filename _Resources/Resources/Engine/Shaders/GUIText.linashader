/*
This file is a part of: Lina Engine
https://github.com/inanevin/LinaEngine

Author: Inan Evin
http://www.inanevin.com

Copyright (c) [2018-] [Inan Evin]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#LINA_RP_FINAL
#LINA_PIPELINE_GUI

#LINA_VS

#version 460

layout (location = 0) in vec2 pos;
layout (location = 1) in vec2 uv;
layout (location = 2) in vec4 col;

layout (location = 0) out vec4 fCol;
layout (location = 1) out vec2 fUV;

layout(set = 2, binding = 0) uniform MaterialData
{
   mat4 projection;
   vec4 outlineColor;
   float outlineThickness;
   float softness;
   float thickness;
   int flipAlpha;
   int outlineEnabled;
   int textType;
} material;

void main()
{
   fCol = col;
   fUV = uv;
   gl_Position = material.projection * vec4(pos.x, pos.y, 0.0f, 1.0);
}

#LINA_END

#LINA_FS

#version 460

layout (location = 0) in vec4 fCol;
layout (location = 1) in vec2 fUV;

layout (location = 0) out vec4 fragColor;

layout(set = 2, binding = 0) uniform MaterialData
{
   mat4 projection;
   vec4 outlineColor;
   float outlineThickness;
   float softness;
   float thickness;
   int flipAlpha;
   int outlineEnabled;
   int textType;
} material;

layout(set = 2, binding = 1) uniform sampler2D diffuse;

void main()
{
   vec4 finalColor = fCol;

   // Simple text
   if(material.textType == 0)
   {
      float a = texture(diffuse, fUV).r;
      finalColor = vec4(fCol.rgb, a * fCol.a);
   }
   else if(material.textType == 1)
   {
      // SDF text
      float d = texture(diffuse, fUV).r;
      float alpha = smoothstep(material.thickness - material.softness, material.thickness + material.softness, d);
      vec3 baseColor = fCol.rgb;
      if(material.outlineEnabled == 1)
      {
         float border = smoothstep(material.thickness + material.outlineThickness - material.softness, material.thickness + material.outlineThickness + material.softness, d);
         baseColor = mix(material.outlineColor, fCol, border).rgb;
      }
      finalColor = vec4(baseColor, material.flipAlpha == 1 ? 1.0f - alpha : alpha);
   }

   fragColor = finalColor;
}

#LINA_END