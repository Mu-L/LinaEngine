/*
This file is a part of: Lina Engine
https://github.com/inanevin/LinaEngine

Author: Inan Evin
http://www.inanevin.com

Copyright (c) [2018-] [Inan Evin]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#LINA_SHADER_SKY

#lina_vs

struct LinaVertexToFrag
{
   vec2 uv;
   vec3 worldPos;
   vec3 vNormal;
};

// do not modify
struct LinaVertexInput
{
   vec3 pos;
   vec3 normal;
   vec2 uv;
   uint argument1;
};

// do not modify
struct LinaVertexOutput
{
   vec4 pos;
   LinaVertexToFrag toFrag;
};


void LINA_VERTEX_MAIN(in LinaVertexInput inVertex, out LinaVertexOutput outVertex)
{
   outVertex.pos = vec4(inVertex.pos, 1.0);
   outVertex.toFrag.uv = inVertex.uv;
   outVertex.toFrag.vNormal = inVertex.normal;
   outVertex.toFrag.worldPos = inVertex.pos;
}

#lina_end

#lina_fs

struct LinaVertexToFrag
{
   vec2 uv;
   vec3 worldPos;
   vec3 vNormal;
};

// do not modify
struct LinaFragInput
{
   uint argument1;
   LinaVertexToFrag fromVertex;
};

// do not modify
struct LinaFragOutput
{
   vec4 color;
};

struct LinaMaterial
{
   vec3 skyColor;
   vec3 horizonColor;  
   vec3 groundColor;
   float horizonPosition;
   float horizonDiffusion;
   vec2 sunPosition;
   vec4 sunColor;
   float sunSize;
   float sunDiffusion;
};

#define DEBUG bool(0)

#define GRID_COLOR vec4(1.0,1.0,1.0,0.25)


vec4 AddGrid(vec3 col, vec3 p, float size, float thickness)
{
   vec2 sph = vec2(atan(p.x, p.z) / 3.141519 * 3.0, pow(abs(p.y) + 0.5, 2.0));

   sph *= size;
   float grid =      max(abs(fract(sph.y) - 0.5) * 2.0,
                     abs(fract(sph.x) - 0.5) * 2.0) *
                     (2.0 / thickness) - (2.0 / thickness - 0.75);

   sph /= 4.0;
   float thickGrid = max(abs(fract(sph.y) - 0.5) * 2.0,
                     abs(fract(sph.x) - 0.5) * 2.0) *
                     (2.0 / thickness) - (2.0 / thickness - 0.6);

   grid = clamp(max(grid, thickGrid), 0.0 ,1.0);
   grid *= 1.0-abs(p.y);

   return vec4(mix(col.rgb, 1.0 - col.rgb , grid * GRID_COLOR.a), 1.0);
}

/// --- Sky Shader --- ///
void LINA_FRAG_MAIN(in LinaFragInput inFrag, out LinaFragOutput outFrag)
{

   LinaMaterial material = LINA_GET_MATERIAL(inFrag.argument1);

   vec2 uv = inFrag.fromVertex.uv;
   vec3 worldPos = inFrag.fromVertex.worldPos;
   vec3 vNormal = normalize(inFrag.fromVertex.vNormal);

   /// --- CLAMP PARAMETERS --- ///
   vec3 groundColor = clamp(material.groundColor, 0.0, 1.0);
   vec3 skyColor = clamp(material.skyColor, 0.0, 1.0);
   vec3 horizonColor = clamp(material.horizonColor, 0.0, 1.0);
   vec4 sunColor = clamp(material.sunColor, 0.0, 1.0);
   float sunSize = clamp(material.sunSize, 0.0, 1.0);
   float sunDiffusion = clamp(material.sunDiffusion, 0.2,16.0);

   /// --- HORIZON SHAPE --- ///
   float horizonShape = vNormal.y + material.horizonPosition;
   horizonShape = clamp(1.0 - abs(horizonShape / material.horizonDiffusion / 2.0), 0.0, 1.0);
   float horizonPos = worldPos.y - material.horizonPosition / 2.0;

   /// --- SUN SHAPE --- ///
   float sunShape = 1.0 - dot(vNormal , normalize(worldPos - vec3(material.sunPosition, 1.0)));
   sunShape = 1.0 / sunShape;
   sunShape = sunShape * material.sunSize * 0.01;
   sunShape = clamp(pow(sunShape, sunDiffusion), 0.0, 1.0);

   /// --- BLEND GROUND & SKY & HORIZON --- ///
   vec3 groundSky = mix(groundColor, skyColor, clamp(horizonPos * 1500., 0.0,1.0));
   vec3 worldColor = mix(groundSky, horizonColor,horizonShape);
   vec3 mixed = mix(worldColor, sunColor.rgb, sunShape * sunColor.a);
   outFrag.color = vec4(mixed, 1.0);

   /// --- Debug Grid -- ///
   // outFrag.color = DEBUG ? AddGrid(outFrag.color.rgb, vNormal.xyz, 16.0, 0.1) : outFrag.color.rgba;

/*
   float bottomLimit = material.horizonDiffusion;
   float y = (vNormal.y + 1.0) * 0.5;
   float bottomThresh = 0.5 + material.horizonDiffusion;
   float topTresh = 0.5 - material.horizonDiffusion;

   float angle = y;
   float hor = material.horizonPosition;
   float dif = material.horizonDiffusion;
   vec3 target = angle > hor ? material.groundColor : material.skyColor;
   float t = clamp(abs(angle - hor) * dif, 0.0, 1.0);
   outFrag.color = vec4(mix(material.horizonColor, target, t), 1.0);
*/

}

#lina_end

