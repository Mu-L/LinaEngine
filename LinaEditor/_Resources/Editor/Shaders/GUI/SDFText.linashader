/*
This file is a part of: Lina Engine
https://github.com/inanevin/LinaEngine

Author: Inan Evin
http://www.inanevin.com

Copyright (c) [2018-] [Inan Evin]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#lina_variant
#name : Default
#blend: LINA_BLEND_ALPHA
#depth: LINA_DEPTH_NONE
#target: LINA_TARGET_LDR
#cull: LINA_CULL_NONE
#face: LINA_FACE_CCW
#lina_end

#lina_vs

#include "Resources/Core/Shaders/Common/GlobalData.linashader"
#include "Resources/Editor/Shaders/Common/RenderPass_GUI.linashader"

layout (location = 0) in vec2 pos;
layout (location = 1) in vec2 uv;
layout (location = 2) in vec4 col;

layout (location = 0) out vec4 outCol;
layout (location = 1) out vec2 outUV;
layout (location = 2) flat out uint outMaterialIndex;

layout( push_constant ) uniform constants
{
	uint argument0;
} PushConstants;

void main()
{
   outCol = col;
   outUV = uv;
   outMaterialIndex = PushConstants.argument0;
   gl_Position = LINA_VIEW.proj * vec4(pos.x, pos.y, 0.0f, 1.0);
}

#lina_end

#lina_fs

#include "Resources/Core/Shaders/Common/GlobalData.linashader"
#include "Resources/Editor/Shaders/Common/RenderPass_GUI.linashader"

layout (location = 0) in vec4 inCol;
layout (location = 1) in vec2 inUV;
layout (location = 2) flat in uint inMaterialIndex;

layout (location = 0) out vec4 outFragColor;


struct LinaGUIMaterial
{
   LinaTexture2D diffuse;
   vec4 outlineColor;
   float thickness;
   float softness;
   float outlineThickness;
   float outlineSoftness;
};

LinaGUIMaterial GetMaterial(uint index)
{
   LinaGUIMaterial mat;
   mat.diffuse.txt = LINA_EDITOR_MATERIALS.data[index++];
   mat.diffuse.smp = LINA_EDITOR_MATERIALS.data[index++];

   mat.outlineColor.x = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);
   mat.outlineColor.y = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);
   mat.outlineColor.z = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);
   mat.outlineColor.w = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);

   mat.thickness = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);
   mat.softness = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);
   mat.outlineThickness = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);
   mat.outlineSoftness = uintBitsToFloat(LINA_EDITOR_MATERIALS.data[index++]);

   return mat;
}

void main()
{
   LinaGUIMaterial material = GetMaterial(inMaterialIndex);

   float a = texture(sampler2D(LINA_TEXTURES[material.diffuse.txt], LINA_SAMPLERS[material.diffuse.smp]), inUV).r;
   float outline = smoothstep(material.outlineThickness - material.outlineSoftness, material.outlineThickness + material.outlineSoftness, a);
   a = smoothstep(1.0 - material.thickness - material.softness, 1.0 - material.thickness + material.softness, a);

   vec3 albedo = mix(material.outlineColor.rgb, inCol.rgb, outline);
   outFragColor = vec4(albedo, a * inCol.a);
}


#lina_end
