/*
This file is a part of: Lina Engine
https://github.com/inanevin/LinaEngine

Author: Inan Evin
http://www.inanevin.com

Copyright (c) [2018-] [Inan Evin]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#LINA_SHADER_CUSTOM

#lina_vs

#include "Resources/Editor/Shaders/Common/EditorGlobal.linashader"
#include "Resources/Editor/Shaders/Common/EditorPassGUI.linashader"

layout (location = 0) in vec2 pos;
layout (location = 1) in vec2 uv;
layout (location = 2) in vec4 col;

layout (location = 0) out vec4 outCol;
layout (location = 1) out vec2 outUV;
layout (location = 2) flat out uint outMaterialIndex;

void main()
{
   outCol = col;
   outUV = uv;
   outMaterialIndex = 0;
   gl_Position = LINA_VIEW.proj * vec4(pos.x, pos.y, 0.0f, 1.0);
}

#lina_end

#lina_fs

#include "Resources/Editor/Shaders/Common/EditorGlobal.linashader"
#include "Resources/Editor/Shaders/Common/EditorPassGUI.linashader"

layout (location = 0) in vec4 inCol;
layout (location = 1) in vec2 inUV;
layout (location = 2) flat in uint inMaterialIndex;

layout (location = 0) out vec4 outFragColor;

// BufferType 0: Default
// BufferType 1: Gradient
// BufferType 2: Textured
// BufferType 3: Text
// BufferType 4: SDF Text
// BufferType 5: Horizontal hue
// BufferType 6: Vertical hue
// BufferType 7: Circular wheel hue

vec3 hueSaturationToRGB(float hue, float saturation) {
    float angle = hue * 6.0;
    float r = abs(angle - 3.0) - 1.0;
    float g = 2.0 - abs(angle - 2.0);
    float b = 2.0 - abs(angle - 4.0);
    vec3 rgb = clamp(vec3(r, g, b), 0.0, 1.0);
    return mix(vec3(1.0), rgb, saturation); // Mix the RGB color with white based on saturation
}

struct LinaMaterial
{
   vec4 clip;
   float wheelRadius;
   float edgeSmoothness;
};

// 0.5
// 0.005

void main()
{
   LinaMaterial material = LINA_GET_MATERIAL(inMaterialIndex);

   if(material.clip.z > 0 && material.clip.w > 0)
   {
      if(gl_FragCoord.x < material.clip.x || gl_FragCoord.x > material.clip.x + material.clip.z || gl_FragCoord.y < material.clip.y || gl_FragCoord.y > material.clip.y + material.clip.w)
         discard;
   }

    vec2 position = inUV - vec2(0.5, 0.5);
    float d = length(position);

    float angle = atan(position.y, position.x);
    if (angle < 0.0) angle += 2.0 * 3.14159265; 

    float hue = angle / (2.0 * 3.14159265); // Normalize the angle to get the hue
    float saturation = d / material.wheelRadius; // Normalize the distance to get the saturation

     // Calculate alpha for anti-aliasing
    float alpha = 1.0;
    if (d > (material.wheelRadius - material.edgeSmoothness)) {
        alpha = (material.wheelRadius - d) / material.edgeSmoothness;
    }
    outFragColor = vec4(hueSaturationToRGB(hue, saturation) * inCol.rgb, alpha);
}


#lina_end
