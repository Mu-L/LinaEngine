/*
This file is a part of: Lina Engine
https://github.com/inanevin/LinaEngine

Author: Inan Evin
http://www.inanevin.com

Copyright (c) [2018-] [Inan Evin]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#lina_define_variant : default
#lina_pass : rendertarget
#lina_cull : none
#lina_front : cw
#lina_depth : disable
#lina_end

#lina_define_variant : final
#lina_pass : swapchain
#lina_cull : none
#lina_front : cw
#lina_depth : disable
#lina_end

#lina_vs

layout (location = 0) in vec2 pos;
layout (location = 1) in vec2 uv;
layout (location = 2) in vec4 col;

layout (location = 0) out vec4 fCol;
layout (location = 1) out vec2 fUV;

void main()
{
   fCol = col;
   fUV = uv;
   gl_Position = LINA_PROJ * vec4(pos.x, pos.y, 0.0f, 1.0);
}

#lina_end

#lina_fs

layout (location = 0) in vec4 fCol;
layout (location = 1) in vec2 fUV;

layout (location = 0) out vec4 fragColor;

struct MaterialData
{
   vec4 color1;
   vec4 color2;
   vec4 floatPack1;
   vec4 floatPack2;
   LinaTexture2D diffuse;
};

layout(set = 2, binding = 0) readonly buffer MaterialBuffer
{
   MaterialData materials[];
} materialBuffer;


// BufferType 0: Default
// BufferType 1: Gradient
// BufferType 2: Textured
// BufferType 3: Text
// BufferType 4: SDF Text
layout( push_constant ) uniform Constants
{
	int bufferType;
   int materialID;
} constants;

void main()
{
   fragColor = fCol;
   
   MaterialData material = materialBuffer.materials[constants.materialID];

   if(constants.bufferType == 1)
   {
      // floatPack1 = uvMultiply, uvShift
      // floatPack2 = radialInfluence, isAABuffer, radialSize, 0

      // Linear gradient
      float t1 = fUV.x * material.floatPack1.r + fUV.y * material.floatPack1.g;

      // Radial gradient
      float t2 = length((fUV - material.floatPack1.ba) * material.floatPack2.b);

      float t = t1 * (1.0f - material.floatPack2.r) + t2 * (material.floatPack2.r);

      vec4 col = mix(material.color1, material.color2, t);
      float alpha = fCol.a * material.floatPack2.g + col.a * (1.0f - material.floatPack2.g);
      fragColor = vec4(col.rgb, alpha); 
   }
   else if(constants.bufferType == 2)
   {
      // Textured
      // floatPack1 = uvMultiply, uvShift
      // floatPack2 = isAABuffer, 0,0,0

      vec4 col = LinaSampleTexture2D(material.diffuse, fUV * material.floatPack1.rg + material.floatPack1.ba);
      float alpha = fCol.a * material.floatPack2.r + col.a * (1.0f - material.floatPack2.r);
      fragColor = vec4(col.rgb, alpha) * material.color1; 
   }
   else if(constants.bufferType == 3)
   {
      // Simple Text
      // floatPack1 = 0,0,0,0
      fragColor = vec4(fCol.rgb, LinaSampleTexture2D(material.diffuse, fUV).r * fCol.a);
   }
   else if(constants.bufferType == 4)
   {
      // SDF text
      // floatPack1 = thickness, softness, outlineThickness, hasOutline
      // floatPack2 = flipAlpha, 0, 0, 0

      float d = LinaSampleTexture2D(material.diffuse, fUV).r;
      float alpha = smoothstep(material.floatPack1.r - material.floatPack1.g, material.floatPack1.r + material.floatPack1.g, d);
      float border = smoothstep(material.floatPack1.r + material.floatPack1.b - material.floatPack1.g, material.floatPack1.r + material.floatPack1.b + material.floatPack1.g, d);
      vec3 outlinedColor = mix(material.color1, fCol, border).rgb;
      vec3 baseColor = mix(fCol.rgb, outlinedColor, material.floatPack1.a);
      alpha = (1.0f - alpha) * material.floatPack2.r + alpha * (1.0f - material.floatPack2.r);
      fragColor = vec4(baseColor, alpha * fCol.a);
   }

}

#lina_end
